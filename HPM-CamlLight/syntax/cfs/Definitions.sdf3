module cfs/Definitions

imports
	lex/Identifier
	cfs/Types
	cfs/Expressions
	cfs/Patterns
	cfs/Names

context-free start-symbols
	TypeDef
	TypeDefPart
	TypeParams
	ConstrDecl
	LabelDecl
	ExcDef
	ValDef
	ValDecl

template options
	keyword -/- [A-Za-z0-9]

templates
	TypeDef.TypeDef          = [type [TypeDefPart+; separator="and"]]
	
	TypeDefPart.ADTDef       = [[TypeParams] [IDENT] = [ConstrDecl+; separator="|"]]
	TypeDefPart.RecordDef    = [[TypeParams] [IDENT] = {[LabelDecl+; separator=";"]}]
	TypeDefPart.TypeAlias    = [[TypeParams] [IDENT] == [TypeExpr]]
	TypeDefPart.AbstrType    = [[TypeParams] [IDENT]]
	TypeDefPart.MutAbstrType = [[TypeParams] [IDENT] mutable]
	
	TypeParams.None          = []
  TypeParams               = [[TYPEVARIABLE]]
	TypeParams.TypeParams    = [([TYPEVARIABLE+; separator=","])]
	
	ConstrDecl.CConstrDecl   = [[IDENT]]
	ConstrDecl.NCConstrDecl  = [[IDENT] of [TypeExpr]]
	
	LabelDecl.LabelDecl      = [[IDENT] : [TypeExpr]]
	LabelDecl.MutLabelDecl   = [mutable [IDENT] : [TypeExpr]]
	
	ExcDef.ExcDef            = [exception [ExcDecl+; separator="and"]]
	
	// ExcDecl.Exc*             = [[ConstrDecl]]
	ExcDecl.ExcCConstrDecl   = [[IDENT]]
	ExcDecl.ExcNCConstrDecl  = [[IDENT] of [TypeExpr]]
	
	ValDef.ValDef            = [let [GlobalLetBind+; separator="and"]]
	ValDef.RecValueDef       = [let rec [GlobalLetRecBind+; separator="and"]]
	
	GlobalLetBind.GlobalValDef        = [[Pattern] = [Expr]] // prefer the rule below over a ConstrPattern
	GlobalLetBind.GlobalFunDef        = [[Name] [Pattern+; separator=" "] = [Expr]] {prefer}
	GlobalLetRecBind.GlobalRecValDef  = [[Pattern] = [Expr]] // prefer the rule below over a ConstrPattern
	GlobalLetRecBind.GlobalRecFunDef  = [[Name] [Pattern+; separator=" "] = [Expr]] {prefer}
	
	ValDecl.ValDecl          = [value [ValDeclPart+; separator="and"]]
	
	ValDeclPart.ValDeclBind  = [[IDENT] : [TypeExpr]]