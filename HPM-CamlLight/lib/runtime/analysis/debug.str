module runtime/analysis/debug

imports
	
	runtime/index/core
	runtime/index/query
	runtime/task/core
	runtime/task/debug
	runtime/editor/interop

rules
	
  analysis-debug-show-current-partition(|language):
    (_, _, _, path, project-path) -> (filename, result)
    with
    	partition := $[[project-path]/[path]];
      index-setup(|language, project-path);
      filename := <guarantee-extension(|"index.current.aterm")> path;
      result   := <index-get-all-in-partition> partition
      
  analysis-debug-show-all-partitions(|language):
    (_, _, _, path, project-path) -> (filename, result)
    with
    	partition := $[[project-path]/[path]];
      index-setup(|language, project-path);
      filename := <guarantee-extension(|"index.all.aterm")> path;
      result   := <index-get-all-partitions; map(\filename -> (filename, <index-get-all-in-partition> filename)\)>
      
  analysis-debug-show-tasks(|language):
    (_, _, _, path, project-path) -> (filename, result)
    with
      task-setup(|project-path);
    	partition  := $[[project-path]/[path]];
      result-ast := <task-debug-info(|partition)>;
      if result-task := <foreign-call(|"Analysis", "from-task-debug")> result-ast then
      	filename := <guarantee-extension(|"partition.task")> path;
      	result   := result-task
      else
      	filename := <guarantee-extension(|"partition.aterm")> path;
      	result   := result-ast
    	end
      
  analysis-debug-show-tasks-selection(|language):
    (selected, _, _, path, project-path) -> (filename, result)
    with
      task-setup(|project-path);
    	partition  := $[[project-path]/[path]];
      result-ast := <task-debug-ast> selected;
      if result-task := <foreign-call(|"Analysis", "from-task-debug")> result-ast then
      	filename := <guarantee-extension(|"selection.task")> path;
      	result   := result-task
      else
      	filename := <guarantee-extension(|"selection.aterm")> path;
      	result   := result-ast
    	end
      
  analysis-debug-reset-index(|language):
    (_, _, _, path, project-path) -> None()
    with
    	partition := $[[project-path]/[path]];
      index-setup(|language, project-path);
      index-reset
      
  analysis-debug-reset-task(|language):
    (_, _, _, path, project-path) -> None()
    with
      task-setup(|project-path);
      task-reset

  analysis-debug-reanalyze(|language):
    (_, _, _, path, project-path) -> None()
    with
    	partition := $[[project-path]/[path]];
    	task-setup(|project-path);
    	task-reset;
      index-setup(|language, project-path);
      index-reload

