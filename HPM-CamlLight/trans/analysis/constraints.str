module analysis/constraints

imports
  include/CamlLight
  extra-constructors

signature
  constructors
    CEq     : TypeExpr * TypeExpr -> Constraint
    CRefEq  : URI * TypeExpr -> Constraint 

strategies
	fresh-var = !TypeVar(<newname> "TV")
	
	mkCEq = !CEq(<Fst>, <Snd>)
  mkCRefEq = !CRefEq(<Fst>, <Snd>)
  
  unCEq: CEq(a,b) -> (a,b)
  unCRefEq: CRefEq(a,b) -> (a,b)
  
  typevar-first = try(\CEq(a, b@TypeVar(_)) -> CEq(b, a) where <not(?TypeVar(_))> a\)
  
strategies
  consecutive-pairs(s) = Dupl;(id,Tl<+![]);zipr(s)
  
  multi-lookup(|l): n -> <filter(?(n,_));map(Snd)> l
  keys = map(Fst);make-set
  
  min-length(|len): list -> list
    where <length;!(<id>,len);geq> list
  
  is-effective(s): term -> result
    where result := <s> term
        ; <not(eq)>(term,result)
  
  fixpoint(s): a -> b
    where b := <is-effective(s);fixpoint(s) <+ !a> a
    
  scanfix(s): a -> b
    where b := <is-effective(s);\c -> [c | <scanfix(s)> c]\ <+ ![a]> a
    
  scanfix(s|n): a -> b
    where b := <is-effective(s);where(<gt>(n,1));\c -> [c | <scanfix(s|<dec>n)> c]\ <+ ![a]> a