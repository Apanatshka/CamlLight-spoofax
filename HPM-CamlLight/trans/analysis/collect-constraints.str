module analysis/collect-constraints

imports
  include/CamlLight
  extra-constructors
  analysis/constraints
  lib/runtime/nabl/-
  lib/runtime/analysis/-
  lib/runtime/task/-
  lib/runtime/types/-

strategies
  collect-constraint: GlobalValDef(name, body) ->
      [(<try(name-resolve)> name, type, body-e*, body-c*), body-u*]
      where type := <fresh-var>
          ; (body-u*, body-e*, body-c*) := <collect-constraint> (body, type)
  collect-constraint: GlobalRecValDef(name, body) ->
      [(<try(name-resolve)> name, type, body-e*, body-c*), body-u*]
      where type := <fresh-var>
          ; (body-u*, body-e*, body-c*) := <collect-constraint> (body, type)
            
  collect-constraint: (FunctionDef([SimpleMatch(PatternVar(name), body)]), ty) ->
      ([body-u*], [(uri, in-ty) | body-e*], [c | body-c*])
      where in-ty  := <fresh-var>
          ; out-ty := <fresh-var>
          ; type   := FuncType(in-ty, out-ty)
          ; uri    := <try(name-resolve)> name
          ; c      := <mkCEq> (ty, type)
          ; (body-u*, body-e*, body-c*) := <collect-constraint> (body, out-ty)
  
  collect-constraint: (FuncApp(f, a), ty) -> ([u1*,u2*], [e1*,e2*], [c1*,c2*])
    where a-ty := <fresh-var>
        ; f-ty := FuncType(a-ty, ty)
        ; (u1*, e1*, c1*) := <collect-constraint> (f,f-ty)
        ; (u2*, e2*, c2*) := <collect-constraint> (a,a-ty)
  
  collect-constraint: (IfElseExpr(c,t,e), ty) -> ([u1*,u2*,u3*], [e1*,e2*,e3*], [c1*,c2*,c3*])
    where c-ty := ConstrType(None(), GlobalName("bool"))
        ; (u1*, e1*, c1*) := <collect-constraint> (c,c-ty)
        ; (u2*, e2*, c2*) := <collect-constraint> (t,ty)
        ; (u3*, e3*, c3*) := <collect-constraint> (e,ty)
  
  collect-constraint: (p@Prefix(_), ty) -> ([], [], [CEq(ty, <get-type> p)])
  
  collect-constraint: (i@Int(_), ty)    -> ([], [], [CEq(ty, <get-type> i)])
  collect-constraint: (f@Float(_), ty)  -> ([], [], [CEq(ty, <get-type> f)])
  collect-constraint: (c@Char(_), ty)   -> ([], [], [CEq(ty, <get-type> c)])
  collect-constraint: (s@String(_), ty) -> ([], [], [CEq(ty, <get-type> s)])
  collect-constraint: (u@Unit(), ty)    -> ([], [], [CEq(ty, <get-type> u)])
  
  collect-constraint: (Var(gn), ty) -> ([], [], [<mkCRefEq>(<name-resolve> gn, ty)])
  
  name-resolve: name -> uri
    where <has-annotation(?Use(u))> name
        ; Def(uri) := <task-get-results; Hd> u
  name-resolve: name -> uri
    where <has-annotation(?Def(uri))> name
  
  collect-constraints: 
    Module(mp*) -> <mapconcat(cc-helper)> mp*
  cc-helper:
  	MP(ValDef(list)) -> <mapconcat(collect-constraint)> list
  cc-helper:
    MP(RecValueDef(list)) -> <mapconcat(collect-constraint)> list